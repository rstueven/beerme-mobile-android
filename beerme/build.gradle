apply plugin: 'com.android.application'

static def getApiKey(key){
    Properties props = new Properties()
    props.load(new FileInputStream(new File('local.properties')))
    return props[key]
}

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 60
        manifestPlaceholders = [ GOOGLE_MAPS_API_KEY:getApiKey("google.maps.key")]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }


    flavorDimensions = ["version"]

    productFlavors {
        paid {
            dimension "version"
            applicationId "com.beerme.android"
            versionName "2.5.8-60"
        }
        free {
            dimension "version"
            applicationId "com.beerme.android_free"
            versionName "2.5.8-60 (free)"
        }
    }

    // Necessary until minSdkVersion >= 26
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

    }
    lint {
        disable 'MissingTranslation'
    }
    namespace 'com.beerme.android'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'com.google.android.gms:play-services-ads:23.2.0'
    implementation 'com.google.android.gms:play-services-auth:21.2.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.google.android.gms:play-services-maps:19.0.0'
//    implementation project(':lvl')
    implementation 'com.androidmapsextensions:android-maps-extensions:2.4.0'
    implementation project(':aFileDialog')
}